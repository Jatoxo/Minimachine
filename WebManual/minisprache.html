<html>

<head>

    <title>Minimaschine</title>

    <link rel="stylesheet" type="text/css" href="css/contentPage.css">
    <meta http-equiv="content-type" content="text/html; charset=UTF-8">
</head>

<body>

    <h1 align=center>4. Minisprache</h1>
    <h2>4.1 Konzeption der Minisprache</h2>
    <p>Die Minisprache ist von der Syntax her den späteren Wirth-Sprachen (Modula-2, Oberon) nachempfunden.
        Insbesondere sind alle Strukturen selbstklammernd, Variablen werden vor Beginn des Algorithmus vereinbart.
        Die typischen Algorithmuselemente sind vorhanden: Ein- und zweiseitige bedingte Anweisung, Wiederholung mit
        Ein- bzw. Ausgangsbedingung sowie Zählwiederholung.</p>
    <p>Aufgrund der einfachen Zielmaschine sind natürlich Einschränkungen notwendig. Es gibt
        keine Unterprogramme, da diese von der Minimaschine nicht unterstützt werden. Aus dem gleichen Grund
        gibt es auch keine Unterstützung für Kommazahlen. Da auch auf die Einführung logischer
        Werte verzichtet wird, beschränken sich die Variablen auf ganze Zahlen, sodass kein Typenkonzept
        eingeführt werden musste.</p>
    <p>Ergebnisse von Algorithmen müssen wie bei der Assemblerprogrammierung im Speicher abgelegt werden.
        Die Vorbesetzung von Variablen erfolgt direkt zu Beginn eines Programms.</p>
    <h2>4.2 Programmaufbau</h2>
    <p>Jedes Programm besteht prinzipiell aus drei Teilen: Kopf, Variablenvereinbarung und Rumpf.</p>
    <pre>PROGRAM Ggt;
(* Berechnet den GGT zweier Zahlen *)
VAR
    z1, z2;
BEGIN
    (* Startwerte *)
	z1 := 24;
    z2 := 18;
	(* eigentliche Berechnung *)
    WHILE z1 <> z2
    DO
        IF z1 >= z2
        THEN
            z1 := z1 - z2
        ELSE
            z2 := z2 - z1
        END
    END
END Ggt.</pre>
    <p>Der Programmkopf besteht aus dem Schlüsselwort PROGRAM und einem Programmnamen, der am Ende wiederholt wird.</p>
    <p>Die Variablenvereinbarung wird eingeleitet durch das Schlüsselwort VAR, gefolgt von einer durch
        Komma getrennten Liste von Variablennamen.</p>
    <p>Der durch BEGIN und END name eingeschlossene Rumpf enthält den Algorithmus. Alle Anweisungen werden mit
        ";" getrennt. Für Terme werden die üblichen Rechenzeichen verwendet (+, -, *, /), das
        Zeichen "%" berechnet den Rest der ganzzahligen Division. Zuweisungen werden als ":="
        notiert.</p>
    <p> Zwischen den Terminalsymbolen können beliebig Kommentare eingestreut werden. Kommentare beginnen mit
        der Zeichenfolge "(*" und enden mit der Zeichenfolge "*)"; sie können über
        mehrere Zeilen gehen. Kommentare dürfen nicht geschachtelt werden.</p>
    <h2>4.3 Die erweiterte Minisprache</h2>
    <p>Bei Aktivierung der Erweiterungen wird die Minisprache um Felder und um Unterprogramme erweitert.</p>
    <p>Felder werden durch eine eckige Klammer mit Angabe der Feldlänge hinter dem Variablennamen vereinbart.<br>
        Zugriff auf die Feldelemente erfolgt ebenfalls durch eckige Klammern; der Index läuft von 0 bis Länge-1.
        Eine Indexkontrolle findet nicht statt.</p>
    <p>Prozeduren werden nach den Variablen und vor dem Programmrumpf vereinbart. Sie können auf die globalen
        Variablen zugreifen sowie lokale Variable und Parameter vereinbaren.<br>
        Es werden reguläre Prozeduren ohne Rückgabewert (Schlüsselwort PROCEDURE) und Funktionsprozeduren mit
        einer ganzen Zahl als Rückgabewert (Schlüsselwort FUNCTION) unterschieden. Der Aufruf regulärer
        Prozeduren ist eine Anweisung, Funktionsprozeduren werden in Termen verwendet.</p>
    <h2>4.4 Syntax der Minisprache</h2>
    <p>Die vollständige Syntax der Minisprache in EBNF (Erweiterungen sind kursiv gesetzt):</p>
    <pre>
Programm = 
    Kopf <i>{</i> Variablenvereinbarung <i>}</i> <i>{ Prozedurvereinbarung }</i> Rumpf .
Kopf = 
    "PROGRAM" Bezeichner ";" .
Rumpf = 
    "BEGIN" Anweisungsfolge "END" Bezeichner "." .
Variablenvereinbarung = 
    "VAR" Variable { "," Variable } ";" .
Variable =
    Bezeichner <i>[ "[" Zahl "]" ]</i> .
<i>Prozedurvereinbarung =
    ( "PROCEDURE" | "FUNCTION" ) Bezeichner "(" { Paramterliste } ")" [ Variablenvereinbarung ]
    "BEGIN" Anweisungsfolge "END" Bezeichner ";" .
Paramterliste =
    Parameter { "," Parameter } .
Parameter =
    [ "VAR" ] Bezeichner [ "[" Zahl "]" ] .</i>
Anweisungsfolge =
    Anweisung { ";" Anweisung } . 
Anweisung = 
    [ Zuweisung | BedingteAnweisung | WiederholungMitEingangsbedingung | 
    WiederholungMitAusgangsbedingung | ZaehlWiederholung <i>| Prozeduraufruf | ReturnAnweisung</i> ] .
Zuweisung =
    Bezeichner <i>[ "[" Ausdruck "]" ]</i> ":=" Ausdruck .
BedingteAnweisung = 
    "IF" Bedingung "THEN" Anweisungsfolge
    [ "ELSE" Anweisungsfolge ] "END" .
WiederholungMitEingangsbedingung =
    "WHILE" Bedingung "DO" Anweisungsfolge "END" .
WiederholungMitAusgangsbedingung =
    "REPEAT" Anweisungsfolge "UNTIL" Bedingung .
ZaehlWiederholung =
    "FOR" Bezeichner ":=" Ausdruck "TO" Ausdruck
    [ "BY" [ "+" | "-" ] Zahl ] "DO" Anweisungsfolge "END" .
<i>Prozeduraufruf =
    Bezeichner "(" [ Ausdruck { "," Ausdruck } ] ")" .
ReturnAnweisung =
    "RETURN" [ Ausdruck ] .</i>
Bedingung =
    Ausdruck ( "=" | "&lt;&gt;" | "&gt;" | "&gt;=" | "&lt;" | "&lt;=" ) Ausdruck .
Ausdruck =
    [ "+" | "-" ] Term { ( "+" | "-" ) Term } .
Term =
	Faktor { ( "*" | "/" | "%" ) Faktor } .
Faktor =
	Bezeichner <i>[ "[" Ausdruck "]" ]</i> | Zahl | "(" Ausdruck ")" <i>| Funktionsaufruf</i> .
<i>Funktionsaufruf =
    Bezeichner "(" [ Ausdruck { "," Ausdruck } ] ")" .</i>
Bezeichner =
    "A" .. "Z" | "a" .. "z" { "A" .. "Z" | "a" .. "z" | "0" .. "9" | "_" | "$" } .
Zahl =
    "0" .. "9" { "0" .. "9" } . </pre>
    <p>Die Kommentare treten im Syntaxdiagramm nicht auf, da sie entsprechend den Leerzeichen, Tabulatoren oder
        Zeilenwechseln bereits bei der Zerlegung des Quelltextes in Terminalsymbole abgearbeitet werden.</p>
</body>

</html>